static def isSnapshot(version) {
  return version.contains("SNAPSHOT")
}

static def getRepositoryUrl(version) {
  if (isSnapshot(version)) {
    return 'https://oss.sonatype.org/content/repositories/snapshots'
  }
  return 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  ext {
    gsonVersion = '2.8.8'
    vertxVersion = '4.3.1'
  }

  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17

  repositories {
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }

  dependencies {
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.slf4j:slf4j-api:1.7.36'
  }

  group = 'io.contek.invoker'
  version = '3.0.0-SNAPSHOT'

  task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
  }

  task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = "invoker-${project.name}"
        from components.java
        artifact sourcesJar
        artifact javadocJar

        pom {
          afterEvaluate {
            name = project.description
            description = project.description
          }

          url = 'https://github.com/contek-io/io.contek.invoker'

          scm {
            connection = 'scm:git:git://github.com/contek-io/io.contek.invoker.git'
            developerConnection = 'scm:git:ssh://github.com/contek-io/io.contek.invoker.git'
            url = 'http://github.com/contek-io/io.contek.invoker/tree/master'
          }

          licenses {
            license {
              name = 'MIT License'
              url = 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id = 'Fenrur'
              name = 'Fenrur'
//              email = 'dev@contek.io'
              organization = 'Fenrur'
//              organizationUrl = 'https://contek.io'
            }
          }
        }
      }
    }

  }

}
